// SPDX-FileCopyrightText: 2018 Camille019
// SPDX-License-Identifier: MPL-2.0

WHITESPACE = _{ " " | "\t" | ("\\" ~ NEWLINE) }
COMMENT = _{ "#" ~ (!(NEWLINE) ~ ANY)* }

file = {
    SOI ~ NEWLINE* ~
    (statement_block ~ (NEWLINE+ ~ statement_block)*)? ~
    NEWLINE*
}

statement_block = _{ (bus | chip ) }

bus = { "bus" ~ name ~ string }
chip = { "chip" ~ name+ ~
    (NEWLINE+ ~ (label | compute | set | ignore))*
}

label = { "label" ~ name ~ string }
compute = { "compute" ~ name ~ expr ~ "," ~ expr }
ignore = { "ignore" ~ name }
set = { "set" ~ name ~ expr }

name = @{string}
string = @{ string_short | string_quoted }

string_quoted = @{ "\"" ~ string_long ~ "\"" }
string_short = ${ (ASCII_ALPHANUMERIC | "_")+ }
string_long = ${ (!("\"" | NEWLINE) ~ ANY)* }

expr = { operand ~ (operator ~ operand)* }
operand = _{ ("(" ~ expr ~ ")") | function | var }

operator = _{ add | sub | mult | div }
add = { "+" }
sub = { "-" }
mult = { "*" }
div = { "/" }

function = _{ (inv | exp | ln) ~ operand }
inv = { "-" }
exp = { "^" }
ln = { "`" }

var = _{ raw | num }
raw = { "@" }
num = @{ ASCII_DIGIT* ~ ("." ~ ASCII_DIGIT+)? }
// This Source Code Form is subject to the terms of the Mozilla Public
// License, v. 2.0. If a copy of the MPL was not distributed with this
// file, You can obtain one at http://mozilla.org/MPL/2.0/.

WHITESPACE = _{ " " | "\t" | ("\\" ~ NEWLINE) }
COMMENT = _{ "#" ~ (!(NEWLINE) ~ ANY)* }

file = {
    SOI ~ NEWLINE* ~
    (statement_block ~ (NEWLINE+ ~ statement_block)*)? ~
    NEWLINE* ~ EOI
}

statement_block = { (bus | chip ) }

bus = { "bus" ~ name ~ name ~ name }
chip = { "chip" ~ name_list ~
    (NEWLINE+ ~ (label | compute | set | ignore))*
}

label = { "label" ~ name ~ name }
compute = { "compute" ~ name ~ expr ~ "," ~ expr }
ignore = { "ignore" ~ name }
set = { "set" ~ name ~ expr }

name_list = { name+ }

name = @{ string_short | string_quoted }

string_quoted = @{ "\"" ~ string_long ~ "\"" }
string_short = ${ (ASCII_ALPHANUMERIC | "_")+ }
string_long = ${ (!("\"" | NEWLINE) ~ ANY)* }

expr = { operand ~ (operator ~ operand)* }
operand = _{ ("(" ~ expr ~ ")") | function | var }

operator = _{ add | sub | mult | div }
add = { "+" }
sub = { "-" }
mult = { "*" }
div = { "/" }

function = { (inv | exp | ln) ~ operand }
inv = { "-" }
exp = { "^" }
ln = { "`" }

var = _{ raw | num }
raw = { "@" }
num = @{ ASCII_DIGIT* ~ ("." ~ ASCII_DIGIT+)? }